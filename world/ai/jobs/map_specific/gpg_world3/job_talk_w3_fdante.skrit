/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_w3_fdante.skrit

	Author(s)	: 	Eric Tams, Sarah Boulian

	Purpose		: 	Custom talk job for Dante in the fiery realms of the Yesterhaven World.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	choose conversation
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END );
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE );
	}

	event OnEnterState$
	{
		#only( game )
		[[
		m_Go$.GetConversation.RSSelectConversation( "conversation_dante" );
		if ( Victory.IsQuestCompleted( "quest_cup" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_dante_complete" );
		}
		else if( m_Target$.IsValid )
		{
			Go Cup$ = m_Target$.Go.Inventory.GetItemFromTemplate( "gpg_chalice_glb_01" );
			if( Cup$ != null )
			{
				if( GameAuditor.GetDb.GetBool( "c_0x041001b0" ) )
				{
					Cup$.Parent.Mind.SDoJob( MakeJobReq( JAT_DROP, JQ_ACTION, QP_FRONT, AO_REFLEX, Cup$.goid ) );
					Cup$.Aspect.SSetIsVisible( false );
					PostWorldMessage( WE_REQ_DELETE, Cup$.goid, Cup$.goid, .1 );
					m_Go$.GetConversation.RSSelectConversation( "conversation_dante_complete" );
				}
				else
				{
					m_Go$.GetConversation.RSSelectConversation( "conversation_dante_cup" );
				}
			}
		}
		]]
		if( m_Go$.Mind.ActorAutoFidgets )
		{
			StartFidgetIfRequired$();
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}

////////////////////////////////////////////////////////////////////////////////
//	start fidget, delete job.
state Exiting$
{
	event OnEnterState$
	{
		if( m_Mind$.ActorAutoFidgets )
		{
			StartFidgetIfRequired$();
		}
		m_Job$.MarkForDeletion();
	}
}